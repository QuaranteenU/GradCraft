import kr.entree.spigradle.attribute.*

plugins {
    id 'java'
    id 'kr.entree.spigradle' version '1.2.4'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.ebean' version '11.40.1'
}

group 'university.quaranteen'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    enginehub()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://ci.mg-dev.eu/plugin/repository/everything' }
    maven { url 'https://repo.citizensnpcs.co/' }
}

spigot {
    authors = ['Quaranteen University Team']
    apiVersion = '1.15'
    load = Load.POST_WORLD
    depends = ['BKCommonLib', 'WorldGuard']
    name = 'GradCraft'
    description = 'A plugin for QU'
    website = 'https://quaranteen.university/'
    commands {
        diploma {
            description = 'Diploma command'
            usage = '/diploma <name> <major>'
        }
        dbdiploma {
            description = 'Get your diploma from the database!'
            usage = '/dbdiploma'
        }
        cerlist {
            description = 'List available ceremonies'
            usage = '/cerlist'
            permission = 'gc.ceremony.runner'
        }
        cerstart {
            description = 'Start a ceremony'
            usage = '/cerstart <id>'
            permission = 'gc.ceremony.runner'
            permissionMessage = 'You don\'t have permission!'
        }
        cerstop {
            description = 'Stop the currently running ceremony'
            usage = '/cerstop'
            permission = 'gc.ceremony.runner'
            permissionMessage = 'You don\'t have permission!'
        }
        cerclaim {
            description = 'Become the show runner for the running ceremony'
            usage = '/cerclaim'
            permission = 'gc.ceremony.runner'
            permissionMessage = 'You don\'t have permission!'
        }
        cernext {
            description = 'Advance to the next graduate'
            usage = '/cernext'
            permission = 'gc.ceremony.runner'
            permissionMessage = 'You don\'t have permission!'
        }
        cerstatus {
            description = 'Show the ceremony status'
            usage = '/cerstatus'
            permission = 'gc.ceremony.runner'
            permissionMessage = 'You don\'t have permission!'
        }
        robes {
            description = 'Get graduation robes!'
            usage = '/robes <red|black|blue>'
            permission = 'gc.ceremony.graduate'
            permissionMessage = 'You don\'t have permission!'
        }
    }
    permissions {
        'gc.ceremony.*' {
            defaults = 'op'
            children = [
                    'gc.ceremony.runner': true,
                    'gc.ceremony.graduate': true
            ]
        }
        'gc.ceremony.runner' {
            defaults = 'op'
        }
        'gc.ceremony.graduate' {
            defaults = true
        }
    }
}

tasks.build.dependsOn tasks.shadowJar

dependencies {
    annotationProcessor 'io.ebean:querybean-generator:11.39.3'
    testImplementation 'junit:junit:4.12'
    compileOnly paper('1.15.2')
    compileOnly 'com.bergerkiller.bukkit:BKCommonLib:1.15.2-v4-SNAPSHOT'
    compileOnly worldedit()
    compileOnly worldguard()
    implementation 'mysql:mysql-connector-java:8.0.20'
    implementation 'com.zaxxer:HikariCP:3.4.5'
    compileOnly 'net.citizensnpcs:citizens:2.0.26-SNAPSHOT'
}